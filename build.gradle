buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

allprojects {
    group = 'org.mapsforge'
    version = 'master-SNAPSHOT'

    ext.gdxVersion = "1.9.10"
    ext.gwtVersion = "2.8.2"
    ext.slf4jVersion = "1.7.28"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }
}

static def androidCompileSdk() { return 31 }

// 14 for Support Library, 16 for sqlite-android
static def androidMinSdk() { return 21 }

static def androidTargetSdk() { return 30 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava9Compatible()) {
            if (!project.properties.containsKey('android')) {
                options.compilerArgs.addAll(['--release', '7'])
            }
        }
    }
}
