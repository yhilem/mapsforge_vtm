plugins {
    id 'com.android.application' version '8.10.1' apply false
}

allprojects {
    group = 'org.mapsforge'
    version = 'master-SNAPSHOT'

    ext.mapsforgeVersion = "0.25.0"
    ext.gdxVersion = "1.11.0"
    ext.gwtVersion = "2.8.2"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }
}

static def androidCompileSdk() { return 36 }

static def androidMinSdk() { return 21 }

static def androidTargetSdk() { return 36 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava9Compatible()) {
            if (!project.properties.containsKey('android')) {
                options.compilerArgs.addAll(['--release', '8'])
            }
        }
    }
}
