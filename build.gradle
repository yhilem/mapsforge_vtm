plugins {
    id 'com.android.application' version '8.1.4' apply false
}

allprojects {
    group = 'org.mapsforge'
    version = 'master-SNAPSHOT'

    ext.gdxVersion = "1.11.0"
    ext.gwtVersion = "2.8.2"
    ext.slf4jVersion = "1.7.28"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }
}

static def androidCompileSdk() { return 34 }

// 14 for Support Library
static def androidMinSdk() { return 21 }

static def androidTargetSdk() { return 34 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava9Compatible()) {
            if (!project.properties.containsKey('android')) {
                options.compilerArgs.addAll(['--release', '8'])
            }
        }
    }
}
