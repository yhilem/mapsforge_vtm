apply plugin: 'com.android.application'

dependencies {
    implementation project(':vtm-android')
    implementation project(':vtm-extras')
    implementation project(':vtm-themes')
    // https://github.com/square/okhttp/issues/4481
    implementation 'com.squareup.okhttp3:okhttp:3.12.13'
}

android {
    namespace 'org.oscim.app'
    compileSdk androidCompileSdk()

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        versionCode project.versionCode()
        versionName project.versionName()
        minSdk androidMinSdk()
        targetSdk androidTargetSdk()
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            file("${rootDir}/vtm-android/natives").eachDir() { dir ->
                jniLibs.srcDirs += "${dir.path}/lib"
            }
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lint {
        abortOnError false
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
}
