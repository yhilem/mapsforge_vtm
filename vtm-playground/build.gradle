apply plugin: 'application'

dependencies {
    implementation project(':vtm-desktop-lwjgl')
    file("${rootDir}/vtm-desktop/natives").eachDir() { dir ->
        implementation files(dir.path)
    }
    implementation project(':vtm-extras')
    implementation project(':vtm-gdx-poi3d')
    implementation project(':vtm-hillshading')
    implementation project(':vtm-http')
    //implementation project(':vtm-jeo')
    implementation project(':vtm-json')
    implementation project(':vtm-jts')
    implementation project(':vtm-models')
    implementation project(':vtm-mvt')
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "org.mapsforge:mapsforge-map-awt:$mapsforgeVersion"
}

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
}

application {
    run {
        if (project.hasProperty("args")) {
            args project.getProperty("args").split(",")
        }
    }

    task fatJar(type: Jar, dependsOn: configurations.runtimeClasspath) {
        archiveClassifier = 'jar-with-dependencies'
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/services/io.jeo.data.Driver'
        manifest {
            attributes 'Main-Class': 'org.oscim.test.MapsforgeTest'
        }
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        with jar
    }
}
