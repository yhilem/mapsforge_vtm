apply plugin: 'application'

dependencies {
    implementation project(':vtm-desktop-lwjgl')
    file("${rootDir}/vtm-desktop/natives").eachDir() { dir ->
        implementation files(dir.path)
    }
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation 'com.fifesoft:rsyntaxtextarea:2.6.1'
    implementation 'com.jtattoo:JTattoo:1.6.11'

    implementation 'org.mapsforge:mapsforge-core:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-map:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-map-awt:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-map-reader:master-SNAPSHOT'
    implementation 'org.mapsforge:mapsforge-themes:master-SNAPSHOT'
    implementation 'net.sf.kxml:kxml2:2.3.0'
}

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
}

application {
    mainClass = "org.oscim.theme.comparator.Main"

    task fatJar(type: Jar, dependsOn: configurations.runtimeClasspath) {
        archiveClassifier = 'jar-with-dependencies'
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
        manifest {
            attributes 'Main-Class': "${mainClass}"
        }
        with jar
    }
}
